// The AWT facade offers a 2D alternative view of Terasology (on PCs)

// Grab all the common stuff like plugins to use, artifact repositories, code analysis config
apply from: "$rootDir/config/gradle/common.gradle"

import org.apache.tools.ant.filters.FixCrLfFilter
import java.text.SimpleDateFormat;

def dateTimeFormat = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssXXX")
dateTimeFormat.timeZone = TimeZone.getTimeZone("UTC")

ext {
    // General props
    mainClassName = 'org.terasology.engine.TerasologyAwt'
    subDirLibs = 'libs'
    templatesDir = new File(rootDir, 'templates')
    rootDirDist = new File(rootDir, 'build/distributions')

    // Read environment variables, including variables passed by jenkins continuous integration server
    env = System.getenv()

    // Version related
    startDateTimeString = dateTimeFormat.format(new Date())
    versionFileName = 'VERSION'
    versionBase = new File(templatesDir, "version.txt").text.trim()
    displayVersion = versionBase
}

dependencies {
    compile project(':engine')
}

// Change the output dir
sourceSets {
    main {
        java {
            output.classesDir 'build/classes'
        }
        output.resourcesDir 'build/classes'
    }
}

// Instructions for packaging a jar file for the AWT facade
jar {
    manifest {
        //TODO: Maybe later add the engine's version number into here?
        def manifestClasspath = "$subDirLibs/" + configurations.runtime.collect { it.getName() }.join(" $subDirLibs/")
        attributes("Main-Class": mainClassName, "Class-Path" : manifestClasspath, "Implementation-Title": "Terasology-" + project.name, "Implementation-Version": env.BUILD_NUMBER + ", " + env.GIT_BRANCH + ", " + env.BUILD_ID)
    }
}

task awt(type:JavaExec) {
    description = "Run 'Terasology' to play the game as an awt application"

    // Dependencies: natives + all modules & the AWT facade itself (which will trigger the engine)
    dependsOn rootProject.extractNatives
    dependsOn rootProject.moduleClasses
    dependsOn classes

    // Run arguments
    main = mainClassName
    workingDir = rootDir
    String[] runArgs = ["-homedir"]
    args runArgs

    // Classpath: AWT itself, engine classes, engine dependencies. Not modules or natives since the engine finds those
    classpath sourceSets.main.output.classesDir
    classpath project(':engine').sourceSets.main.output.classesDir
    classpath project(':engine').configurations.runtime
}

// Preps a version file to bundle with AWT dists. This eventually goes into the root of a zip file
task createVersionFile(type: Copy) {
    inputs.property('dateTime', startDateTimeString)
    onlyIf { env.BUILD_URL != null }
    from templatesDir
    into "$buildDir"
    include versionFileName
    expand(buildNumber: env.BUILD_NUMBER, buildUrl: env.BUILD_URL, gitBranch: env.GIT_BRANCH, dateTime: startDateTimeString, displayVersion: displayVersion)
    filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
}

// Main application dist target. Does NOT include any modules.
task distApp (type: Sync) {
    description = "Creates an application package for distribution"

    dependsOn createVersionFile
    dependsOn rootProject.extractNatives
    dependsOn jar

    into ("$distsDir/app")
    from ("$rootDir/Heightmap.txt") {}
    from ("$rootDir/README.markdown") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
        rename('README.markdown', 'README')
    }
    from ("$rootDir/LICENSE") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
    }
    from ("$rootDir/NOTICE") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
    }
    from ("build/libs/Awt.jar") {
        rename 'AWT.*', 'Terasology-awt.jar'
    }
    from('launchScripts') {
        exclude('*.xml')
        exclude('TeraEd.exe')
    }
    from("$buildDir/$versionFileName") {}

    into(subDirLibs) {
        from configurations.runtime
        from project(':engine').jar
    }
    into(dirNatives) {
        from "$rootDir/$dirNatives"
    }
}

// This is a TEMPORARY tweak to make "changing" dependencies always ('0') check for newer snapshots available
// TODO: Remove this when versioning and promotion works fully, then we shouldn't care about snapshots normally anyway
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Distribute modules - with the option to provide a list of additional modules to include as dependencies
// Note how in a local developer workspace this might grab more than Core, but in Jenkins always only Core
// Example command including additional modules: gradlew -PextraModules="Signalling,BlockNetwork"
task distModules (type: Sync) {
    description = "Prepares modules for distribution"
    //dependsOn distApp
    dependsOn rootProject.moduleJars

    // So this is probably a hack, but it works ;-) It does not work if it is in distApp, default "into" quirk ?
    into("$distsDir/app/modules")
    rootProject.terasologyModules().each {
        from "$rootDir/modules/${it.name}/build/libs"
        include "*.jar"
    }

    // If the execution was supplied with an extraModules property then include those modules as well
    if (project.hasProperty('extraModules')) {
        extraModules.split(',').each { String dependency ->
            println "Extra module: " + dependency

            // See if the supplied extra already exists in the local workspace, in that case don't add it again
            if (findProject(':modules:' + dependency) != null) {
                println "Found supplied extra $dependency already present in local workspace, ignoring"
            } else {
                dependencies {
                    // TODO: When version handling and promotion is in then we can probably remove "changing"
                    compile group: 'org.terasology.modules', name: dependency, version: '+', changing: true
                }
            }
        }

        // Note: This causes early resolution of artifacts! Can mess with order of execution/classpath and cause issues
        // Example: gradlew distApp copyModules -PextraModules="Signalling" fails due to to lacking engine on classpath
        // But the error goes away if either distApp is removed or this following statement isn't reached
        configurations.compile.resolvedConfiguration.resolvedArtifacts.each { ResolvedArtifact artifact ->
            def id = artifact.moduleVersion.id
            if (id.group == 'org.terasology.modules') {
                println "Remotely resolved $id.group - $id.name at version $id.version"
                from artifact.file
                into("$distsDir/app/modules")
            }
        }
    }
}

task distAppZip (type: Zip) {
    from "$distsDir/app"
    baseName = "TerasologyAWT"
}

task distForLauncher (type: Sync) {

    into rootDirDist
    from distAppZip

    into ("../resources/main/org/terasology/version") {
        from ("$rootDir/engine/build/classes/org/terasology/version") {
            include ('versionInfo.properties')
        }
    }
}

// Prep an IntelliJ module for the facade
idea {
    module {
        // Change around the output a bit
        inheritOutputDirs = false
        outputDir = file('build/classes')
        testOutputDir = file('build/testClasses')
    }
}

// Note the use of rootProject.idea as the .iws is only available there
rootProject.idea {

    // Generate a run configuration for the this facade's AWT way of running the game
    workspace.iws.withXml { xmlProvider ->

        def runManager = xmlProvider.asNode().find { it.@name == 'RunManager' }
        runManager.append(new XmlParser().parseText('''
                <configuration default="false" name="TerasologyAWT" type="Application" factoryName="Application">
                  <extension name="coverage" enabled="false" merge="false" runner="idea">
                    <pattern>
                      <option name="PATTERN" value="org.terasology.engine.*"/>
                      <option name="ENABLED" value="true"/>
                    </pattern>
                  </extension>
                  <option name="MAIN_CLASS_NAME" value="org.terasology.engine.TerasologyAwt"/>
                  <option name="VM_PARAMETERS" value="-Xms256m -Xmx1024m"/>
                  <option name="PROGRAM_PARAMETERS" value="-homedir"/>
                  <option name="WORKING_DIRECTORY" value="file://$PROJECT_DIR$"/>
                  <option name="ALTERNATIVE_JRE_PATH_ENABLED" value="false"/>
                  <option name="ALTERNATIVE_JRE_PATH" value=""/>
                  <option name="ENABLE_SWING_INSPECTOR" value="false"/>
                  <option name="ENV_VARIABLES"/>
                  <option name="PASS_PARENT_ENVS" value="true"/>
                  <module name="AWT"/>
                  <envs/>
                  <RunnerSettings RunnerId="Debug">
                    <option name="DEBUG_PORT" value=""/>
                    <option name="TRANSPORT" value="0"/>
                    <option name="LOCAL" value="true"/>
                  </RunnerSettings>
                  <RunnerSettings RunnerId="Run"/>
                  <ConfigurationWrapper RunnerId="Debug"/>
                  <ConfigurationWrapper RunnerId="Run"/>
                  <method/>
                </configuration>
            '''))

        // This part puts the added run config into the actual "Run" drop-down
        runManager.append(new XmlParser().parseText('''
            <list size="1">
            <item index="0" class="java.lang.String" itemvalue="Application.TerasologyPC"/>
            </list>
            '''))
    }
}

task copyEclipseLauncher(type: Copy) {
	from "$rootDir/config/eclipse"
	into projectDir
	include("TerasologyAWT.launch")
}

tasks.eclipse {
	dependsOn copyEclipseLauncher
	dependsOn rootProject.extractNatives
}

cleanEclipse.doLast {
    new File(projectDir, "TerasologyAWT.launch").delete()
}
